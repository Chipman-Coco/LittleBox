import com.chipman.buildsrc.*

apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.dagger.hilt.android'

android {
    namespace "com.chipman.littlebox"
    compileSdk ProjectBuildConfig.compileSdkVersion

    defaultConfig {
        applicationId ProjectBuildConfig.applicationId
        minSdk ProjectBuildConfig.minSdkVersion
        targetSdk ProjectBuildConfig.targetSdkVersion
        versionCode ProjectBuildConfig.versionCode
        versionName ProjectBuildConfig.versionName
        testInstrumentationRunner DependencyConfig.Android.AndroidJUnitRunner

        ndk {
            /**
             * 设置支持的SO库架构（'armeabi', 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'）
             */
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'
        }
    }

    buildFeatures {
        viewBinding = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // 自定义打包apk的文件名
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (outputFileName != null && outputFileName.endsWith('.apk')) {
                outputFileName = "${ProjectBuildConfig.applicationId}" +
                        "_${ProjectBuildConfig.versionCode}" +
                        "(${ProjectBuildConfig.versionName})" +
                        "_${variant.buildType.name}" +
                        ".apk"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation(project(":common"))
    implementation(project(":domain"))
    implementation(project(":data"))

    implementation(DependencyConfig.Android.SwiperRefresh)
    implementation(DependencyConfig.Prevalent.FlexBox)
    implementation(DependencyConfig.Jetpack.Hilt)
    kapt(DependencyConfig.Jetpack.HiltAPT)

    testImplementation(DependencyConfig.Android.TestExtJunit)
    androidTestImplementation(DependencyConfig.Android.Junit)
    androidTestImplementation(DependencyConfig.Android.TestEspressoCore)
}